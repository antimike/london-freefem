load "msh3"
load "tetgen"
load "medit"
load "iovtk"
load "ffrandom"
include "/home/user/Source/FreeFEM/freefem-macros/geometry.idp"
include "/home/user/Source/FreeFEM/freefem-macros/constants.idp"
include "/home/user/Source/FreeFEM/freefem-macros/vector-analysis.idp"
include "/home/user/Source/freefem_matlab_octave_plot-public/release-v2.0/demos/ffmatlib.idp"
include "MeshSurface.idp"


// PARAMETERS

//real Mu0 = 4.*pi*1.e-7;	// Vacuum magnetic permeability (SI)
//real Mu0 = 4*pi;
real B0 = 100; // External field strength
real fluxQuantum = 20.7;	// Stuart units: Microns + Gauss

// GEOMETRY

// Bounding box
int FRONT=1, RIGHT=2, BACK=3, LEFT=4, BOTTOM=5, TOP=6;
int EXTERNAL=100, INTERNAL=110;
real boxLength = 50;
int nx=25, ny=25, nz=25;

int[int] BoxN = [nx, ny, nz];
macro Interval [-boxLength/2, boxLength/2]//EOM
real [int, int] BoxBounds = [Interval, Interval, Interval];
int [int, int] BoxLabels = [[LEFT, RIGHT], [FRONT, BACK], [BOTTOM, TOP]];

meshS BBox = SurfaceHex(BoxN, BoxBounds, BoxLabels, 1);

//medit("Bounding box", BBox);

// Slab (simpler construction)
real scaleFactor = 10;
real aspectRatio = 10;
real slabLength = boxLength/scaleFactor;
real slabHeight = slabLength/aspectRatio;
int SLAB = 10;
int nRatio = 5;

int[int] SlabN = (nRatio/scaleFactor)*[nx, ny, (1/aspectRatio)*nz];

real [int, int] SlabBounds = (1/scaleFactor)*[Interval, Interval, (1/aspectRatio)*Interval];
int [int, int] SlabLabels = [[SLAB, SLAB], [SLAB, SLAB], [SLAB, SLAB]];

meshS SlabBdy = SurfaceHex(SlabN, SlabBounds, SlabLabels, -1);

//medit("SC mesh", SlabBdy);

// COMBINED MESH

int SPACE = 30;
int SC = 301;
real maxvolSpace = boxLength^3/(nx*ny*nz);
real maxvolSlab = maxvolSpace/(scaleFactor^3*aspectRatio*nRatio^3);
real[int] regions = [0, 0, slabHeight*1.5, SPACE, maxvolSpace, 0, 0, 0, SC, maxvolSlab];

meshS TsTotal = BBox + SlabBdy;
mesh3 TvTotal = tetg(TsTotal, switch = "pqaAAYYQ", nbofregions = 2, regionlist = regions);
TvTotal = checkmesh(TvTotal);

//medit("Full triangulation", TvTotal);

// FE SPACES

fespace Sv0(TvTotal, P0);
fespace Sv1(TvTotal, P1);
fespace Vv2(TvTotal, [P2, P2, P2]);


// PROBLEM CONSTANTS

real penaltyConstant = 1.;
real penetrationFraction = .1;
real penetrationDepth = penetrationFraction*slabLength;
real inverseLambda = 1/penetrationDepth;
real JsPrefactor = 1/(Mu0*penetrationDepth^2);

// MESH FUNCTIONS

// NOTE: Naming conventions on vector components (i.e., vector `Foo` has components `[Foox, Fooy, Fooz]`) 
// are necessary for vector macros (Grad, Div, Curl, etc.) to work
Sv0 inSC = region == SC;             	// Characteristic function of the SC
Sv1 Theta;                           	// Scalar phase from Cordier 1 (rescaled; cf. eq. (60))
Vv2 [Ax, Ay, Az] = [0, 0, 0];        	// Vector potential
Vv2 [Wx, Wy, Wz];                    	// Vector basis functions
Sv1 w;                               	// Scalar basis functions
Vv2 [Bx, By, Bz] = Curl3d(A);        	// Magnetic field
Vv2 [Hxx, Hxy, Hxz] = [0, 0, 1];     	// External (applied) magnetic field
Vv2 [Jx, Jy, Jz] = inSC * Curl3d(B); 	// Current density (only defined in SC)

// PROBLEM DEFINITION

problem meissner([Ax, Ay, Az, Theta], [Wx, Wy, Wz, w], solver=CG) = 
	int3d(TvTotal)(
		Curl3d(W)'*Curl3d(A) - Div3d(W)*Div3d(A)
		+ penaltyConstant*Div3d(W)*Div3d(A)
		+ inSC*inverseLambda^2*[Wx, Wy, Wz]'*[Ax, Ay, Az]
		- inSC*inverseLambda*[Wx, Wy, Wz]'*Grad3d(Theta)
	)
	+ int3d(TvTotal)(
			-1*inSC*inverseLambda*Grad3d(w)'*[Ax, Ay, Az]
			+ inSC*Grad3d(w)'*Grad3d(Theta)
	)
	+ int2d(TsTotal, EXTERNAL)(
			[Wx, Wy, Wz]'*parallelBdyComponents(Hx)
	)
	+ on(TOP, BOTTOM, Az = 0)
	+ on(LEFT, RIGHT, Ax = 0)
	+ on(FRONT, BACK, Ay = 0);

meissner;

mesh CrossSection = square(nx, ny, 
	SquareParameterization(
		-boxLength/2, boxLength/2, 
		-boxLength, boxLength/2
	)
);
fespace SsCrossSect (CrossSection, P2); 
SsCrossSect BxMid, ByMid;
BxMid = Bx(x, y, 0);
ByMid = By(x, y, 0);
plot([BxMid, ByMid], wait=true);

